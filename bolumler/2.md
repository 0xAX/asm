# X86_64 Assembly'a merhaba deyin [bölüm 2]
Birkaç gün önce ilk blog gönderisini yazdım - x64 assembly giriş - x64 Assembly’e merhaba deyin [bölüm 1] büyük ilgi çekmesi beni çok şaşırttı:

![hackernews](https://github.com/furkanonder/asm/blob/master/bolumler/resimler/hacker_news_post.png)
![hackernews2](https://github.com/furkanonder/asm/blob/master/bolumler/resimler/hacker_news_post2.png)

Benim öğrenme yolumu açıklamak için beni daha da motive ediyor. Bu günlerde farklı insanlardan birçok geri bildirim aldım.
Çok minnettar sözler vardı, ama benim için daha önemli olan, birçok tavsiye ve yeterli eleştirmenin var olmasıydı.
Özellikle harika geri bildirimleri için teşekür etmek istediğim:<br>
  [Fiennes](http://www.reddit.com/user/Fiennes)<br>
  [Grienders](https://disqus.com/by/Universal178/)<br>
  [nkurz](https://news.ycombinator.com/user?id=nkurz)<br>
ve Reddit ve Hacker News'de tartışmaya katılan herkes. Pek çok görüş vardı, ilk kısım mutlak başlangıç için çok net değildi,bu yüzden daha bilgilendirici yazılar yazmaya karar verdim. Yani, x86_64 assembly’e merhaba deyin ikinci bölüm ile başlayalım.
# Terminoloji ve Kavramlar
Yukarıda yazdığım gibi, farklı insanlardan, ilk yazının bazı bölümlerinin net olmadığına dair pek çok geri bildirim aldım.
Bu yüzden, bu ve sonraki bölümlerde göreceğimiz bazı terminolojik açıklamalardan başlayalım.

**Register** - işlemcinin içinde küçük bir miktar depolama alanıdır.İşlemcinin ana noktası veri işlemedir. İşlemci hafızadan veri alabilir, ancak yavaş bir işlemdir. İşte bu yüzden, işlemci register denilen kendi dahili sınırları olan veri depolama setine sahiptir.

**Little endian**- hafızayı büyük bir dizi olarak hayal edebiliriz. Bayt içerir. Her adres bellek “dizisi” nin bir elemanını saklar. Her öğe bir bayttır. Örneğin 4 baytımız var: AA 56 AB FF. Küçük endianda en az anlamlı byte en küçük adrese sahiptir:
```
    0 FF
    1 AB
    2 56
    3 AA
```
0,1,2 ve 3 hafıza adresleridir.

**Big-endian** - Big endian little endiana göre baytları ters sırada saklar. Yani, AA 56 AB FF bayt dizisine sahip olursak:
```
    0 AA
    1 56
    2 AB
    3 FF
```
**Syscall(Sistem Çağrısı)** - Bir kullanıcı seviyesinde programın işletim sistemi için bir şey yapmasını istediği yoldur. Burada [syscall tablosunu](https://github.com/torvalds/linux/blob/master/arch/x86/entry/syscalls/syscall_64.tbl) bulabilirsiniz.

**Stack(Yığın)** - işlemcinin çok kısıtlı bir register sayısı vardır.Bu yüzden, stack sürekli adreslenebilir özel register RSP,SS,RIP ve vb. alanıdır.Sonraki bölümlerde stack'e daha yakından bakacağız.

**Bölüm(section)** - her assembly programı bölümlerden oluşur.Aşağıdaki bölümler vardır:<br>
**data section**, başlangıç verilerinin veya sabitlerin bildirilmesi için kullanılır.<br>
**bss section**, ilk değer atanmamış değişkenleri bildirmek için kullanılır<br>
**text section**, kod için kullanılır.<br>

Genel amaçlı registerlar - 16 genel amaçlı register vardır - rax, rbx, rcx, rdx, rbp, rsp, rsi, rdi, r8, r9, r10, r11, r12, r13, r14, r15. Elbette, assembly programlaması ile ilgili terimlerin ve kavramların tam listesi değildir.Bir sonraki blog yazılarında başka garip ve yabancı bir kelime ile karşılaşırsak, bu kelimelerin açıklaması olacaktır.

# Veri Tipleri
Temel veri türleri bayt, word, doublewords, quadwords ve double quadwords'tir. Bir bayt sekiz bittir, bir word 2 bayttır, bir doubleword 4 bayttır, bir quadword 8 bayttır ve bir double quadword 16 bayttır (128 bit).

Şimdi yalnızca tam sayılarla çalışacağız, o zaman görelim. İki tür tamsayı var: işaretli ve işaretsiz.İşaretleri tamsayılar ve işaretsiz ikili sayılar byte, word, doubleword ve quadword içeririr.Değerleri, bir işaretsiz bayt tamsayı için 0 ile 255 arasında değişir,işaretsiz bir word tamsayısı için 0'dan 65,535'e,işaretsiz bir doubleword tamsayısı için 0'dan 2 ^ 32 - 1'e ve işaretsiz bir quadword tamsayısı için 0 ile 2 ^ 64 - 1 arasında değişir.İşaretli tamsayılar ve işaretli ikili sayılar işaretsiz byte,word ve benzerlerinde tutulur.İşaret biti negatif tamsayılar için atanır ve pozitif tamsayılar ve sıfır için temizlenir.Tamsayı değerleri bayt tamsayısı için –128 ila +127 arasında değişir, bir word tamsayı için –32,768'den +32,767'ye,bir doubleword tamsayısı için –2 ^ 31 ila + 2 ^ 31 - 1 arası,ve bir quadword tamsayısı için –2 ^ 63 ila + 2 ^ 63 - 1 arasında.

# Sections(Bölümler)
Yukarıda yazdığım gibi, her assembly programı bölümlerden oluşur, data section, text section ve bss section olabilir.
Data section'a bakalım. Ana nokta - başlangıç sabitlerini bildirmek. Örneğin:
```
section .data
    num1:   equ 100
    num2:   equ 50
    msg:    db "Sum is correct", 10
 ```
Tamam, neredeyse her şey burada açık. Num1, num2, msg ve değerleri olan 3 sabit 100, 50 ve "Sum is correct",10. Ama db, equ nedir? Aslında, NASM bir dizi sözde talimatı desteklemektedir:

DB, DW, DD, DQ, DT, DO, DY ve DZ - verilerinin bildirilmesi için kullanılır. Örneğin:
```
; Initialize 4 bytes 1h, 2h, 3h, 4h
db 0x01,0x02,0x03,0x04
```
```
;; Initialize word to 0x12 0x34
dw    0x1234
```
RESB, RESW, RESD, RESQ, REST, RESO, RESY ve RESZ – ilk değer atanmayan değişkenleri bildirmek için kullanılır

NCBIN - Harici İkili Dosyaları içerir
EQU - sabitleri tanımlar. Örneğin:
```
;; now one is 1
one equ 1
```
TIMES - Yinelenen Talimatlar veya Veriler.(açıklama sonraki gönderilerde olacak)

# Aritmetik işlemler
Aritmetik talimatların kısa bir listesi:
```
ADD - tamsayı ekle
SUB - çıkarma
MUL - işaretli çarpma
IMUL - işaretsiz çarpıldı
DIV - işaretsiz bölme
IDIV - işaretli bölme
INC - artma
DEC - azaltma
NEG - negatif
```
Bazılarını bu yazıda pratikte göreceğiz. Diğer sonraki gönderilerde ele alınacaktır.
# Kontrol Akışı
Genellikle programlama dilleri değerlendirme sırasını değiştirme yeteneğine sahiptir (if deyimi, case statement, goto vb) ve assembly de de vardır.Burada bazılarını göreceğiz. İki değer arasında karşılaştırma yapmak için cmp komutu vardır.Karar verme için koşullu atlama talimatı ile birlikte kullanılır. Örneğin:
```
;;50 ile rax'ı karşılaştır
cmp rax, 50
```
Cmp komutu sadece 2 değeri karşılaştırır, ancak bunları etkilemez ve karşılaştırma sonucuna bağlı olarak hiçbir şey yürütmez.Karşılaştırmadan sonra herhangi bir işlem yapmak için koşullu atlama yönergeleri vardır. Bunlardan biri olabilir:
```
JE - eşitse
JZ - sıfır ise
JNE - eşit değilse
JNZ - sıfır değilse
JG - ilk işlenen ikinci işlenenden büyükse
JGE - ilk işlenen ikinci veya daha büyükse
JA - JG ile aynı, ancak işaretsizler için karşılaştırma yapar
JAE - JGE ile aynı, ancak işaretsizsizler için karşılaştırma yapar
```
Örneğin, C dilinde if else ifadesi gibi bir şey yapmak istiyorsak:
```
if (rax != 50) {
    exit();
} else {
    right();
}
```
Assembly dilinde olsaydı:
```
;;50 ile rax'ı karşılaştır
cmp rax, 50
;;Eğer rax 50'ye eşit değilse .exit 'i gerçekleştir.
jne .exit
jmp .right
```
Sözdizimi ile koşulsuz atlama da vardır:
```
JMP label
```
Örneğin:
```
_start:
    ;; ....
    ;;Bir şeyler yap ve .exit etiketine atla
    ;; ....
    jmp .exit

.exit:
    mov    rax, 60
    mov    rdi, 0
    syscall
```
Burada, _start etiketinden sonra olacak bir kodumuz olabilir ve bu kodun tamamı çalıştırılır, Assembly kontrolu .exit etiketine transfer eder ve kod .exit den sonra çalışmaya başlayacaktır.
<br>
Genellikle koşulsuz atlama döngüleri kullanır. Örneğin, etiketimiz ve ondan sonra bir kodumuz var.
Bu kod koşul durumdan başka her şeyi çalıştırır ve koşul başarılı olmazsa bu kodun başlangıcına atlar.
Döngüler sonraki bölümlerde ele alınacaktır.
# Örnek
Basit bir örnek görelim.
İki tam sayı alır, bu sayıların toplamını alır ve önceden tanımlanmış bir sayıyla karşılaştırır.Eğer önceden tanımlanmış sayı toplama eşitse ekrana bir bir şey yazdırılır,eğer değilse sadece programdan çıkar.
İşte örneğimizin kaynak kodu:
```
section .data
    ; Sabitleri tanımla
    num1:   equ 100
    num2:   equ 50
    ; mesajı ata
    msg:    db "Toplam doğru\n"

section .text

    global _start

;; giriş noktası
_start:
    ; num1 değerini rax ‘a ata
    mov rax, num1
    ; num2 değerini rbx‘a ata
    mov rbx, num2
    ; rax ve rbx toplamını al, ve değerini rax olarak sakla.
    add rax, rbx
    ; rax ve 150 karşılaştır.
    cmp rax, 150
    ;  rax ve 150 eşit değilse .exit etiketine git
    jne .exit
    ; rax ve 150 eşitse .rightSum  etiketine git
    jmp .rightSum

; Toplam doğru ise mesajı yazdır
.rightSum:
    ;;write syscall(sistem çağrısı)
    mov   rax, 1
    ;;file descritor(dosya tanıtıcı), standart çıktı
    mov   rdi, 1
    ;;mesajın adresi
    mov   rsi, msg
    ;;mesajın uzunluğu
    mov   rdx, 15
    ;;write syscall(sistem çağrısı) çağır
    syscall
    ; programdan çık
    jmp .exit

; çıkış prosedürü
.exit:
    ; exit syscall
    mov    rax, 60
    ; çıkış kodu
    mov    rdi, 0
    ; exit syscall(sistem çağrısı) çağır
    syscall
```
Kaynak kodu üzerinden gidelim. İlk olarak, iki sabite sahip num1, num2 ve msg değişkenin sahip olduğu "Toplam doğru\n" içeren veri bölümü vardır.Şimdi 14'üncü satıra bakalım.Burası programın giriş noktasıdır.Num1 ve num2 değerlerini genel amaçlı registerlar olan rax ve rbx'e aktarırız.Şimdi rax registerında num1 ve num2 toplamı var.
<br>
Tamam, değeri 100 olan num1 ve değeri 50 olan num2'ye sahibiz.Toplamımız 150 olmalı.Cmp komutuyla kontrol edelim.Rax ile 150 değerini karşılaştırdıktan sonra karşılaştırma sonucunu kontrol ediyoruz, eğer rax 150'ye eşit değilse (jne ile kontrol ederek) .exit etiketine gidiyoruz, eğer eşitse, .rightSum etiketine gidiyoruz.
<br>
Şimdi iki etiketimiz var: .exit ve .rightSum.Birincisi, rax için 60'tır, çıkış  için sistem çağrı numarasıdır ve rdi  0'dır, bu bir çıkış kodudur.İkincisi .rightSum oldukça kolaydır, sadece toplam doğru yazdırır.
