# X86_64 Assembly'e merhaba deyin [bölüm 4]
Bir süre önce x86_64 için assembly programlaması hakkında bir dizi blog yazısı yazmaya başladım. Bunu asm etiketi ile bulabilirsiniz.Maalesef son zamanlarda meşguldüm ve yeni yazı yoktu, bu yüzden bugün assembly hakkında yazılar yazmaya devam ediyorum ve her hafta bunu yapmaya çalışacağım.

Bugün string ve bazı string işlemlerine bakacağız. Hala nasm assembler ve linux x86_64 kullanıyoruz.
## Ters String
Assembly programlama dili hakkında konuştuğumuzda tabiki string veri türünden bahsedemeyiz aslında bayt dizisi ile ilgileniyoruz.Basit bir örnek yazmayı deneyelim, string verilerini tanımlayacağız ve stdout'a sonuçları ters çevirip yazacağız.Yeni programlama dilini öğrenmeye başladığımızda bu görevler oldukça basit ve popüler gözüküyor. Uygulamaya bakalım.

Her şeyden önce, ilk değer verilmiş olan veriyi tanımlarım.Veri bölümüne(Data Section) yerleştirilecektir(bölümler hakkında önceki yazıları okuyabilirsiniz):
```
section .data
    SYS_WRITE   equ 1
    STD_OUT     equ 1
    SYS_EXIT    equ 60
    EXIT_CODE   equ 0

    NEW_LINE    db 0xa
    INPUT   db "Merhaba dunya!"
```
Burada dört sabiti görebiliriz:
* SYS_WRITE - ‘write’ syscall numarası
* STD_OUT - stdout file descriptor(dosya tanıtıcısı)
* SYS_EXIT - ‘exit’ syscall numarası
* EXIT_CODE - exit kodu

Syscall(sistem çağrısı) listesini bulabilirsiniz - burada .Ayrıca orada tanımlanmış:
* NEW_LINE - yeni satır (\ n) sembolü
* INPUT - giriş stringimiz, tersine çevireceğimiz
Sonra buffer için bss bölümünü tanımladık, ters stringi koyacağız:
```
section .bss
    OUTPUT  resb 1
```
Tamam, sonuç alacağımız bazı veri ve bufferlarımız var, şimdi kod için text bölümünü tanımlayabiliriz.Ana program \_start'dan başlayalım:

```
_start:
    mov rsi, INPUT
    xor rcx, rcx
    cld
    mov rdi, $ + 15
    call stringUzunluguHesapla
    xor rax, rax
    xor rdi, rdi
    jmp tersString
```
İşte bazı yeni şeyler. Nasıl çalıştığını görelim:İlk olarak 2 inci satırda Stdout'a yazarken yaptığımız gibi  INPUT adresini rsi registerına yazdık.Ve rcx registerına sıfır yazdık.Stringimizin uzunluğunu hesaplamak için sayaç olacak.4. satırda cld operatörünü görebiliriz.Df bayrağını sıfırlar.İçine ihtiyacımız var çünkü string uzunluğunu hesaplayacağız, bu stringin sembollerinden geçeceğiz ve eğer df bayrağı 0 olacaksa stringin sembollerini soldan sağa ele alacağız.Ardından calculateStrLength fonksiyonunu çağırıyoruz. 5. satırdaki mov rdi, $ + 15 talimatını unuttum, biraz sonra anlatırım. Ve şimdi, hesaplaStrLength uygulamasına bakalım:
```
stringUzunluguHesapla:
    ; kontrol et, stringin sonu mu?
    cmp byte [rsi], 0
    ; Evet ise programdan çık
    je programdanCikis
    ; rsi'den al'e bayt yükle ve rsi'ı artır.
    lodsb
    ; sembolü stack'e pushla
    push rax
    ; sayacı artır
    inc rcx
    ; tekrar dön
    jmp stringUzunluguHesapla
```
Adından da anlaşılacağı gibi, sadece INPUT stringinin uzunluğunu  hesaplar ve  sonucunu rcx registerında saklar.Her şeyden önce, rsi registerının sıfıra işaret etmediğini kontrol etmeliyiz, eğer bu stringin sonu ise ve fonksiyondan çıkabiliyorsak.Sıradaki lodsb komutu.Basit, sadece 1 bayt (low part of 16 bit ax)  al registerine koy ve rsi işaretçisini değiştir.Cld komutunu çalıştırdığımız gibi lodsb her zaman rsi'yi bir bayt soldan taşır bu yüzden string sembolleri taşırız.Sonra rax'ın değerini stack'e pushlarız şimdi stringimizden sembol içeriyor(lodsb puts byte from si to al, al is low 8 bit of rax).Sembolü stack'e neden pushladık?Stack'in nasıl çalıştığını hatırlamalısınız, LIFO (last input, first output) prensibine göre çalışır.Bu bizim için çok iyi.İlk sembolü si'den alacağız stack'e pushlayacağız sonra ikinciden ve vb.Böylece stack'in en üstünde string sembolü olacak.Daha sonra sembolü stack'ten sembol ile pop ederiz ve OUTPUT buffer'ına yazarız.Ondan sonra sayacımızı (rcx) artırırız ve tekrar döngünün başlangıcına döneriz.
<br>
Tamam, tüm sembolleri stringden stack'e pushladık, şimdi exitFromRoutine'e atlayarak \_start 'a geri dönebiliriz. Nasıl yapılır? Bunun için ret komutumuz var. Fakat eğer kod şöyle olacaksa:
```
programdanCikis:
    ;_start'a geri dön
    ret
```
Çalışmayacak. Niye? Bu karmaşık. Hatırlayın \_start da  calculateStrLength'i çağırdık. Bir fonksiyon çağırdığımızda ne olur?İlk olarak, tüm fonksiyon parametreleri sağdan sola stack'e pushlanır.Adres döndükten sonra stack'e pushlanır.Böylece fonksiyon çalıştırma bitiminden sonra nereye döneceğini bilecektir. Ama hesaplaStrLength'e bakın sembolleri stringimizden stack'e pushladık ve şimdi stack'in üstünde geri dönüş adresi yok ve fonksiyon nereye döneceğini bilmiyor. Bununla nasıl olur?Şimdi çağırmadan önce garip komutlara bir göz atmalıyız:
```
mov rdi, $ + 15
```
Her şeyden önce:

* $ - $'ın tanımladığı string'in hafızadaki pozisyonunu döndürür.
* $$ - o anki bölüm başlangıcının hafızadaki konumunu döndürür.

Demek ki mov rdi, $ + 15 için konumumuz var ama neden buraya 15 ekleyelim? Bak, calculatorStrLength'den sonraki satırın konumunu bilmemiz gerekiyor. Dosyamızı objdump util ile açalım:
```
objdump -D reverse

reverse:     file format elf64-x86-64

Disassembly of section .text:

00000000004000b0 <_start>:
  4000b0:	48 be 41 01 60 00 00 	movabs $0x600141,%rsi
  4000b7:	00 00 00
  4000ba:	48 31 c9             	xor    %rcx,%rcx
  4000bd:	fc                   	cld
  4000be:	48 bf cd 00 40 00 00 	movabs $0x4000cd,%rdi
  4000c5:	00 00 00
  4000c8:	e8 08 00 00 00       	callq  4000d5 <calculateStrLength>
  4000cd:	48 31 c0             	xor    %rax,%rax
  4000d0:	48 31 ff             	xor    %rdi,%rdi
  4000d3:	eb 0e                	jmp    4000e3 <reverseStr>
 ```
Burada, satır 12'nin (mov rdi, $ + 15) 10 bayt ve satır 16'daki fonksiyon çağrısının 5 bayt aldığını görebiliriz,böylece 15 bayt alır.Bu nedenle dönüş adresimiz mov rdi, $ + 15 olacaktır. Şimdi rdi'den stack'e dönüş adresini pushlayabilir ve fonksiyondan geri dönebiliriz:
```
programdanCikis:
    ; geri dönüş adresini tekrar stack'e pushla
    push    rdi
    ; _start'a geri dön
    ret
```
Şimdi başlangıca dönebiliriz.CalculateStrLength çağrısından sonra rax ve rdi'ye sıfır yazıp reverseStr etiketine atlıyoruz.Uygulaması aşağıdaki gibidir:
```
tersString:
    ; stringin sonu olup olmadığını kontrol et
    cmp rcx, 0
    ; Eğer evet ise stringi yazdır
    je  printResult
    ; sembolü stackten al
    pop rax
    ; output buffer'a yaz.
    mov [OUTPUT + rdi], rax
    ; sayacın uzunluğunu azalt
    dec rcx
    ; ek olarak sayacı arttır (write syscall için)
    inc rdi
    ; tekrar döngü
    jmp tersString
```
Burada stringin uzunluğu olan sayacımızı kontrol ediyoruz ve eğer sıfır ise tüm sembolleri buffer'a yazdık ve yazdırabiliriz.Sayacı kontrol ettikten sonra, ilk sembolü stackten rax'a pop ederiz ve OUTPUT buffera yazarız.Rdi'yi ekleriz çünkü aksi halde buffer'ın ilk baytına sembol yazarız.Bundan sonra OUTPUT buffer ile sonraki hamle için rdi'yi arttırır, uzunluk sayacını düşürür ve etiketin başlangıcına atlarız.
<br>
ReverseStr komutunun çalıştırılmasından sonra OUTPUT buffer içinde ters string'e sahibiz ve sonucu stdout'a yeni bir satırla yazabiliriz:
```
printResult:
    mov rdx, rdi
    mov rax, 1
    mov rdi, 1
    mov rsi, OUTPUT
    syscall
    jmp yeniSatirYazdir

yeniSatirYazdir:
    mov rax, SYS_WRITE
    mov rdi, STD_OUT
    mov rsi, NEW_LINE
    mov rdx, 1
    syscall
    jmp cikis
```
ve programımızdan çıkalım:
```
cikis:
    ; syscall sayısı
    mov rax, SYS_EXIT
    ; exit kodu
    mov rdi, EXIT_CODE
    ; sys_exit'i çağır
    syscall	
```
Hepsi bu kadar, şimdi programımızı şununla derleyebiliriz:
```
all:
	nasm -g -f elf64 -o ters.o ters.asm
	ld -o ters ters.o
clean:
	rm ters ters.o
```
ve çalıştırın:

![screenshot](https://github.com/furkanonder/asm/tree/master/bolumler/resimler/screenshot.png)

# String işlemleri
Elbette string/bayt manipülasyonları için başka birçok komut vardır:

* REP - rcx sıfır değilken tekrar et
* MOVSB -  bayt stringi kopyalama (MOVSW, MOVSD ve benzeri.)
* CMPSB - bayt stringi karşılaştırması
* SCASB - byte stringi taraması
* STOSB - baytı stringe yazdır
